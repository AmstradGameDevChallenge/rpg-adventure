10 '  
20 ' RPG GAME
30 ' by @dfreniche
40 ' July 2019
50 '

100 ' INITIALIZE
110 DEFINT a-z
120 energy=100:attack=30:defense=15
130 energyen=90:attacken=20:defenseen=10:a$=""

200 ' PRESENTATION
210 MODE 1
211 ' DEFINE SPRITES
212 GOSUB 1300
215 GOSUB 1000
216 GOSUB 1100
220 PRINT"PRESS ENTER TO START"
230 INPUT"",a$
240 CLS

300 ' PRINT GAME STATUS
310 WHILE 1 
320 LOCATE 1,1: PRINT CHR$(251);" PLAYER ["energy"](a"attack")(d"defense")  "
330 LOCATE 1,2: PRINT CHR$(250);" ENEMY  ["energyen"](a"attacken")(d"defenseen")"

400 ' PLAYER ACTIONS
410 LOCATE 1,5: INPUT"ACTION (A/D)";a$: a$=UPPER$(a$)
420 IF a$="A" THEN GOSUB 490:GOTO 510
430 IF a$="D" THEN LOCATE 1,4: ?"Player defends!  ":energy=energy+defense:GOTO 510
440 GOTO 410

480 ' PLAYER ATTACKS
490 energyen=energyen-attack
495 LOCATE 1,4: ?"Player attacks!  "
496 RETURN

500 ' ENEMY ACTIONS
510 IF RND>0.25 THEN LOCATE 1,4: ?"Enemy attacks!  ":energy=energy-attacken:GOTO 900
520 LOCATE 1,4: ?"Enemy defends!  ":energyen=energyen+defenseen


900 WEND

999 ' PRINT PRESENTATION FROM FILE
1000 OPENIN "PRESENT.TXT"
1010 WHILE NOT EOF
1020 INPUT #9, l$: ?l$
1030 WEND
1040 CLOSEIN
1050 RETURN

1099 ' PRINT LINE OF SPRITES
1100 FOR N=1 TO 40
1110 IF N MOD 2=0 THEN ?CHR$(250); ELSE ?CHR$(251);
1120 NEXT N
1130 ?""
1140 RETURN

1299 ' SPRITE DEFINITION
1300 SYMBOL AFTER 250
1310 ' ENEMY
1320 SYMBOL 250,56,108,84,238,198,186,254,170
1330 ' MONSTER
1340 SYMBOL 251,56,59,59,18,126,18,124,68
1350 RETURN

10000 ' LEAVE THIS EMPTY LINE AT THE END FOR UNIX2DOS
